// SPDX-License-Identifier: MIT LICENSE

pragma solidity ^0.8.9;

import "@openzeppelin/contracts/utils/math/SafeCast.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
import "@openzeppelin/contracts/utils/introspection/IERC165.sol";

import {GAME_LOGIC_CONTRACT_ROLE, NAME_TRAIT_ID, DESCRIPTION_TRAIT_ID, IMAGE_TRAIT_ID, MANAGER_ROLE} from "../Constants.sol";

import {ITraitsConsumer} from "../interfaces/ITraitsConsumer.sol";
import {ITokenURIHandler} from "../tokens/ITokenURIHandler.sol";
import {GameRegistryConsumer} from "../GameRegistryConsumer.sol";

/** @title Contract that lets a child contract access the TraitsProvider contract */
abstract contract TraitsConsumer is
    ITraitsConsumer,
    GameRegistryConsumer,
    IERC165
{
    using Strings for uint256;

    /// @notice Override URI for the NFT contract. If not set, on-chain data is used instead
    string public _overrideURI;

    /// @notice Pointer to the handler for TokenURI calls
    address public tokenURIHandler;

    /// @notice Base URI for images, tokenId is appended to make final uri
    string public _baseImageURI;

    /// @notice Base URI for external link, tokenId is appended to make final uri
    string public _baseExternalURI;

    /// @notice Default image URI for the token
    /// @dev Should be set in the constructor
    string public _defaultImageURI;

    /// @notice Default description for the token
    string public _defaultDescription;

    /** ERRORS */

    /// @notice traitsProviderAddress does not implement ITraitsProvvider
    error InvalidTraitsProvider();

    /** SETUP **/

    /** Set game registry  */
    constructor(
        address _gameRegistryAddress,
        uint256 _id
    ) GameRegistryConsumer(_gameRegistryAddress, _id) {}

    /** Sets the override URI for the tokens */
    function setURI(string calldata newURI) external onlyRole(MANAGER_ROLE) {
        _overrideURI = newURI;
    }

    /** Sets base image URI for the tokens */
    function setBaseImageURI(string calldata newURI) external onlyRole(MANAGER_ROLE) {
        _baseImageURI = newURI;
    }

    /** Sets base external URI for the tokens */
    function setBaseExternalURI(string calldata newURI) external onlyRole(MANAGER_ROLE) {
        _baseExternalURI = newURI;
    }

    /** @return Token name for the given tokenId */
    function tokenName(
        uint256 tokenId
    ) external view virtual override returns (string memory) {
        if (_hasTrait(tokenId, NAME_TRAIT_ID)) {
            // If token has a name trait set, use that
            return _getTraitString(tokenId, NAME_TRAIT_ID);
        } else {
            return string(abi.encodePacked("#", tokenId.toString()));
        }
    }

    /** @return Token name for the given tokenId */
    function tokenDescription(
        uint256 tokenId
    ) external view virtual override returns (string memory) {
        if (_hasTrait(tokenId, DESCRIPTION_TRAIT_ID)) {
            // If token has a description trait set, use that
            return _getTraitString(tokenId, DESCRIPTION_TRAIT_ID);
        }

        return _defaultDescription;
    }

    /** @return Image URI for the given tokenId */
    function imageURI(
        uint256 tokenId
    ) external view virtual override returns (string memory) {
        if (_hasTrait(tokenId, IMAGE_TRAIT_ID)) {
            // If token has a description trait set, use that
            return _getTraitString(tokenId, IMAGE_TRAIT_ID);
        }

        if (bytes(_baseImageURI).length > 0) {
            return string(abi.encodePacked(_baseImageURI, tokenId.toString()));
        }

        return _defaultImageURI;
    }

    /** @return External URI for the given tokenId */
    function externalURI(
        uint256 tokenId
    ) external view virtual override returns (string memory) {
        if (bytes(_baseExternalURI).length > 0) {
            return
                string(abi.encodePacked(_baseExternalURI, tokenId.toString()));
        }

        return "";
    }

    /**
     * Sets the tokenURI handler for this token
     *
     * @param handler  Address of the handler contract to use
     */
    function setTokenURIHandler(address handler) external onlyRole(MANAGER_ROLE) {
        tokenURIHandler = handler;
    }

    /** INTERNAL **/

    /**
     * @param tokenId Id of the token to get a trait value for
     * @param traitId Id of the trait to get the value for
     *
     * @return Trait int256 value for the given token and trait
     */
    function _getTraitInt256(
        uint256 tokenId,
        uint256 traitId
    ) internal view returns (int256) {
        return
            _traitsProvider().getTraitInt256(address(this), tokenId, traitId);
    }

    /**
     * @param tokenId Id of the token to get a trait value for
     * @param traitId Id of the trait to get the value for
     *
     * @return Trait string value for the given token and trait
     */
    function _getTraitString(
        uint256 tokenId,
        uint256 traitId
    ) internal view returns (string memory) {
        return
            _traitsProvider().getTraitString(address(this), tokenId, traitId);
    }

    /**
     * @param tokenId        NFT tokenId or ERC1155 token type id
     * @param traitId        Id of the trait to retrieve
     *
     * @return Whether or not the token has the trait
     */
    function _hasTrait(
        uint256 tokenId,
        uint256 traitId
    ) internal view returns (bool) {
        return _traitsProvider().hasTrait(address(this), tokenId, traitId);
    }

    /**
     * Sets the int256 trait value for this token
     *
     * @param tokenId Id of the token to set trait for
     * @param traitId Id of the trait to set
     * @param value   New value of the trait
     */
    function _setTraitInt256(
        uint256 tokenId,
        uint256 traitId,
        int256 value
    ) internal {
        _traitsProvider().setTraitInt256(
            address(this),
            tokenId,
            traitId,
            value
        );
    }

    /**
     * Sets the string trait value for this token
     *
     * @param tokenId Id of the token to set trait for
     * @param traitId Id of the trait to set
     * @param value   New value of the trait
     */
    function _setTraitString(
        uint256 tokenId,
        uint256 traitId,
        string memory value
    ) internal {
        _traitsProvider().setTraitString(
            address(this),
            tokenId,
            traitId,
            value
        );
    }

    /**
     * @notice Generates metadata for the given tokenId
     * @param tokenId  Token to generate metadata for
     * @return A base64 encoded JSON metadata string
     */
    function _tokenURI(
        uint256 tokenId
    ) internal view virtual returns (string memory) {
        // If override URI is set, return the URI with tokenId appended instead of on-chain data
        if (bytes(_overrideURI).length > 0) {
            return string(abi.encodePacked(_overrideURI, tokenId.toString()));
        }

        if (tokenURIHandler == address(0)) {
            return "";
        }

        return
            ITokenURIHandler(tokenURIHandler).tokenURI(
                _msgSender(),
                address(this),
                tokenId
            );
    }

    /**
     * @inheritdoc IERC165
     */
    function supportsInterface(
        bytes4 interfaceId
    ) public view virtual override returns (bool) {
        return
            interfaceId == type(ITraitsConsumer).interfaceId ||
            interfaceId == type(IERC165).interfaceId;
    }
}
